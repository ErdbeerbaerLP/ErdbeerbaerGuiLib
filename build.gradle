buildscript {
    repositories {
        mavenCentral()
        maven { url = "https://files.minecraftforge.net/maven" }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        repositories {
            maven {
                url "https://plugins.gradle.org/m2/"
            }
        }
        maven { url = 'https://sizableshrimp.me/maven' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.32'
    }
}
plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id 'eclipse'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'org.spongepowered.mixin'
repositories {
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
}
version = "2.0.4"
group = "de.erdbeerbaerlp.guilib" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "eguilib"
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'official', version: '1.19.3'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
}
configurations {
    embed
    compileOnly.extendsFrom(embed)
}
shadowJar {
    archiveClassifier = ""
    configurations = [project.configurations.embed]
    relocate 'com.icafe4j', 'guilibshadow.cafe4j'
    relocate 'org.imgscalr', 'guilibshadow.org.imgscalr'
    relocate 'jj2000', 'guilibshadow.jj2000'
    relocate 'org.slf4j', 'guilibshadow.org.slf4j'


}
repositories {
    maven { url 'https://jitpack.io' }
    maven{ url "https://maven.geotoolkit.org"} //Java Advanced Imaging for ImageIO
}
dependencies {
    minecraft 'net.minecraftforge:forge:1.19.3-44.1.16'
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
    embed 'com.github.dragon66:icafe:26c49d5d40'
    embed 'com.github.rkalla:imgscalr:4.2-release'
    embed 'org.mp4parser:isoparser:1.9.41'
}
mixin {
    add sourceSets.main, 'mixins.eguilib.refmap.json'
    config 'mixins.eguilib.json'
}
reobf {
    shadowJar {
        dependsOn tasks.createMcpToSrg
        mappings = tasks.createMcpToSrg.outputs.files.singleFile
    }
}
artifacts {
    archives tasks.shadowJar
}
jar {
    manifest {
        attributes([
                'Maven-Artifact'          : "${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp'               : System.currentTimeMillis(),
                "Specification-Title"     : "eguilib",
                "Specification-Vendor"    : "eguilib",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "eguilib",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

curseforge {
    // $GRADLE_USER_HOME/gradle.properties
    if (project.hasProperty('curseforge.apikey')) {
        apiKey = getProperty("curseforge.apikey")
        project {
            id = '332522'
            changelog = project.changelog
            releaseType = 'beta'
            addGameVersion '1.19.3'
            mainArtifact(jar) {
                displayName = "Erdbeerbaer's GUI Library-$version (MC 1.19.3)"
            }
        }
    }
}
tasks.curseforge.dependsOn(build)
publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'de.erdbeerbaerlp'
            artifactId = 'guilib'
            from components.java
        }
    }
    repositories {
        maven {
            credentials {// $GRADLE_USER_HOME/gradle.properties
                username = project.nexusUser
                password = project.nexusPass
            }
            if(project.version.endsWith('-SNAPSHOT')) {
                url = "https://repo.erdbeerbaerlp.de/repository/maven-snapshots"
            }else{
                url = "https://repo.erdbeerbaerlp.de/repository/maven-releases"
            }
        }
    }
}
task release{

}
tasks.release.dependsOn(tasks.build, tasks.curseforge,tasks.publish)